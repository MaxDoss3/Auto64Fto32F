ext.libpath = file('./').absolutePath

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'com.peterabeles'
    version = '3.0'

    java {
        withJavadocJar()
        withSourcesJar()
        toolchain { languageVersion = JavaLanguageVersion.of(15) }
    }

    ext.build_java8 = true // IntelliJ has issues when this is set to true

    // Creates Java 8 byte code
    if (project.ext.build_java8)
        tasks.withType(JavaCompile).configureEach { options.release = 8 }

    project.ext.lombok_version = '1.18.20'
    project.ext.jabel_version = '0.3.0'
    project.ext.guava_version = '30.1.1-jre'
    project.ext.args4j_version = '2.33'
    project.ext.junit_version = '5.4.0'

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url = 'https://jitpack.io' } // Allows annotations past Java 8 to be used
    }

    test {
        useJUnitPlatform()
        reports.html.enabled = false
    }

    sourceSets {
        examples { java { srcDir 'examples' } }
    }

    dependencies {
        examplesImplementation project.sourceSets.main.compileClasspath
        examplesImplementation project.sourceSets.main.runtimeClasspath

        compileOnly "org.projectlombok:lombok:${lombok_version}"
        compileOnly 'org.jetbrains:annotations:20.0.0' // @Nullable

        testImplementation("org.junit.jupiter:junit-jupiter-api:${junit_version}")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit_version}")

        if (project.ext.build_java8) {
            annotationProcessor("com.github.bsideup.jabel:jabel-javac-plugin:${jabel_version}")
        }
        annotationProcessor "org.projectlombok:lombok:${lombok_version}"
    }

    javadoc { configure(options) { failOnError = false } }

    // if Maven central isn't setup in ~/.gradle/gradle.properties fill in these variables to make it happy
    if (!project.hasProperty('ossrhUsername')) {
        ext.ossrhUsername = "dummy"
        ext.ossrhPassword = "dummy"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = 'Auto64Fto32F'
                    description = 'A minimalist library for converting double (64-bit) numerical Java code into float (32-bit)'
                    url = 'https://github.com/lessthanoptimal/Auto64Fto32F'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                        license {
                            name = 'Public Domain'
                            url = 'http://unlicense.org/'
                        }
                    }
                    developers {
                        developer {
                            id = 'pabeles'
                            name = 'Peter Abeles'
                            email = 'peter.abeles@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/lessthanoptimal/Auto64Fto32F.git'
                        developerConnection = 'scm:git:git://github.com/lessthanoptimal/Auto64Fto32F.git'
                        url = 'https://github.com/lessthanoptimal/Auto64Fto32F'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    if (ext.ossrhPassword != "dummy") {
        signing { sign publishing.publications.mavenJava }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.BIN
    gradleVersion = '6.8.3'
}

